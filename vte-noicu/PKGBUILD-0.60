# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=vte3
_pkgname=(vte3 vte-common)
pkgname=(vte3-noicu vte-common-noicu)
pkgver=0.60.2
pkgrel=2
pkgdesc="Virtual Terminal Emulator widget"
url="https://wiki.gnome.org/Apps/Terminal/VTE"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 pcre2 gnutls fribidi systemd-libs)
#makedepends=(gobject-introspection vala git gtk-doc gperf meson)
makedepends=( git  meson)
_commit=c893450ae32b85870df3313021d9ce1fdfcbe49e  # tags/0.60.2^0
source=("git+https://gitlab.gnome.org/GNOME/vte.git#commit=$_commit")
sha256sums=('SKIP')

replaces=('vte3')
conflicts=('vte3')
provides=('vte3')


pkgver() {
  cd vte
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd vte
}

build() {
  arch-meson vte build -D docs=false -D b_lto=false -D icu=false -D gir=false -D vapi=false
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_vte3-noicu() {
  depends+=(vte-common-noicu)
  provides+=(libvte-2.91.so)

  DESTDIR="$pkgdir" meson install -C build

### Split vte-common
  _pick vte-common "$pkgdir"/etc/profile.d
  _pick vte-common "$pkgdir"/usr/lib/{systemd,vte-urlencode-cwd}
}

package_vte-common-noicu() {
  pkgdesc="Files shared by VTE libraries"
  depends=(sh)
  mv vte-common/* "$pkgdir"
}

# vim:set sw=2 et:
