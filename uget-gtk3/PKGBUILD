# Maintainer (Arch): Balló György <ballogyor+arch at gmail dot com>
# Maintainer: André Silva <emulatorman@hyperbola.info>

pkgname=uget
pkgver=2.2.3
_debver=2.2.3
_debrel=2
pkgrel=1
pkgdesc="GTK+ download manager featuring download classification and HTML import"
arch=('i686' 'x86_64')
url="https://ugetdm.com/"
license=('LGPL-2.1')
#depends=('libnotify' 'gtk3' 'gstreamer' 'curl')
depends=('libnotify' 'gtk3' 'curl')
makedepends=('intltool' 'quilt')
optdepends=('aria2: alternative backend')
source=(https://downloads.sourceforge.net/urlget/$pkgname-$pkgver-1.tar.gz
        https://deb.debian.org/debian/pool/main/u/uget/uget_$_debver-$_debrel.debian.tar.xz)
sha512sums=('SKIP' 'SKIP' )
#            '5267f6a10732a235051055b26058acb8c6b44a4c7fc1d120407b7f5476a615071b45fceba373f8c546304b18894b5c8308c386f57737fdc0e2c14cf4514315fe')

prepare() {
  cd $pkgname-$pkgver
  if [[ ${pkgver%.*} = ${_debver%.*} ]]; then
    # Debian patches
    export QUILT_PATCHES=debian/patches
    export QUILT_REFRESH_ARGS='-p ab --no-timestamps --no-index'
    export QUILT_DIFF_ARGS='--no-timestamps'

    mv "$srcdir"/debian .

    quilt push -av
  fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-gstreamer
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
